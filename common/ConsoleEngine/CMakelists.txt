cmake_minimum_required(VERSION 3.14)
project(ConsoleEngine LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

add_library(ConsoleEngine
    ConsoleEngine.cpp
)
target_include_directories(ConsoleEngine PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
    $<INSTALL_INTERFACE:include>
)
target_compile_features(ConsoleEngine PUBLIC cxx_std_20)

if(CMAKE_CURRENT_SOURCE_DIR STREQUAL CMAKE_SOURCE_DIR)
    option(LOCAL_BUILD "Enable if building without internet (uses common/ dependencies)" OFF)
    enable_testing()

    if(LOCAL_BUILD)
        set(gtest_force_shared_crt ON CACHE BOOL "" FORCE)
        set(gmock_force_shared_crt ON CACHE BOOL "" FORCE)
        add_subdirectory(
            ${CMAKE_CURRENT_SOURCE_DIR}/../googletest
            ${CMAKE_CURRENT_BINARY_DIR}/googletest-build
        )
    else()
        include(FetchContent)
        FetchContent_Declare(
            googletest
            URL https://github.com/google/googletest/archive/refs/tags/v1.14.0.zip
        )
        # Для пользователей: не устанавливаем gtest в систему
        set(gtest_force_shared_crt ON CACHE BOOL "" FORCE)
        FetchContent_MakeAvailable(googletest)
    endif()

    add_subdirectory(tests)
endif()